{ config, lib, pkgs, modulesPath, ... }:
{

  imports = [
    (modulesPath + "/installer/scan/not-detected.nix")
  ];

  hardware.facter.reportPath = ./facter.json;

  boot.kernelPackages = pkgs.linuxPackages_zen;
  boot.initrd.availableKernelModules = [ "nvme" "xhci_pci" "ahci" "thunderbolt" "usbhid" "usb_storage" "sd_mod" "amdgpu" "xhci_hcd" "tpm_crb" ];
  boot.initrd.kernelModules = [ "kvm-amd" "amdgpu" "tpm_crb" "gpd-fan" ];
  boot.kernelModules = [ "kvm-amd" "i2c-dev" "tpm_crb" "gpd-fan" "bmi260_i2c" ];
  boot.kernelParams = [
    "amd_iommu=on"
    "iommu=pt"
    "pcie_aspm=force"
    "rtc_cmos.use_acpi_alarm=1"

    "acpi_os_name=\"Microsoft Windows NT\""
    "acpi_osi=\"Windows 2015\""
  ];
  boot.extraModulePackages = with config.boot.kernelPackages; [ ];
  boot.extraModprobeConfig = "";
  
  # Let EC control decide fan speed, see https://docs.kernel.org/6.18/hwmon/gpd-fan.html
  boot.kernel.sysfs.devices.platform.gpd_fan.hwmon."hwmon[0-9]*".pwm1_enable = 2;

  # Breaks suspend due to ppfeaturemask
  programs.corectrl = {
    enable = lib.mkForce false;
  };
  hardware.amdgpu.overdrive.enable = lib.mkForce false;

  # Touchpad and gamepad "mouse mode"
  services.udev.extraRules = ''
    SUBSYSTEM=="usb", ATTRS{idVendor}=="2f24", ATTRS{idProduct}=="0135", MODE="0666", GROUP="plugdev"
  '';

  # Gyro control
  hardware.sensor.iio.enable = true;

  # Touchpad size calibration, generated by the following command:
  # libinput measure touchpad-size 48x39
  services.udev.extraHwdb = ''
    evdev:name:HTIX5288:00 0911:5288 Touchpad:dmi:*svnGPD:*pnG1617-01**
      EVDEV_ABS_00=12:2624:54
      EVDEV_ABS_01=74:1332:32
      EVDEV_ABS_35=12:2624:54
      EVDEV_ABS_36=74:1332:32
  '';

  # As per https://github.com/aarron-lee/gpd-win-tricks/tree/main/win4-suspend-mods
  systemd.services.gpd-sleep-fix = {
    wantedBy = [ "sleep.target" ];
    before = [ "sleep.target" ];
    serviceConfig.User = "root";
    serviceConfig.Type = "oneshot";
    serviceConfig.RemainAfterExit = "yes";
    unitConfig.StopWhenUnneeded = "yes";
    path = [ pkgs.kmod ];
    script = ''
      echo "Unloading bmi260 modules..."
      modprobe -r bmi260_i2c
      modprobe -r bmi260_core
      echo "Done unloading modules!"
    '';
    postStop = ''
      echo "Loading bmi260 modules..."
      modprobe bmi260_i2c
      modprobe bmi260_core
      echo "Done loading modules!"
    '';
  };

  nixpkgs.hostPlatform = "x86_64-linux";

}